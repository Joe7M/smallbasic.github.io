<h1 id="point">POINT</h1>
<blockquote>
<p>n = POINT (x [, y])</p>
</blockquote>
<p>Returns the color of the pixel at <code>x</code>,<code>y</code>.</p>
<p>If the y-argument is not specified, <code>x</code> specifies the following info-code:</p>
<ul>
<li>0: returns the current graphics cursor X-position</li>
<li>1: returns the current graphics cursor Y-position</li>
</ul>
<h3 id="example-1-get-color">Example 1: Get color</h3>
<pre><code>print point(100,100)        &#39; Output: -2038815</code></pre>
<h3 id="example-2-get-graphics-cursor-position">Example 2: Get graphics cursor position</h3>
<pre><code>x = point(0)
y = point(1)
print &quot;Initial position: &quot;; x, y     &#39; Output: 0,0

line STEP 50,50 STEP 50,50

x = point(0)
y = point(1)
print &quot;Position after STEP: &quot;; x, y  &#39; Output: 100,100</code></pre>
<h3 id="example-3">Example 3</h3>
<p>This demonstrates saving a screen section and redrawing it at different places on screen, a tiling and a moving across the screen.</p>
<pre><code>&#39; POINT demo.bas  SmallBASIC 0.12.2 [B+=MGA] 2016-03-07
for objects = 1 to 300
  if rnd &gt; 0.5 then
    circle rnd*xmax\1, rnd*ymax\1, rnd*50\1, rnd*5, rnd*16/1 
  else
    rect rnd*xmax\1, rnd*ymax\1 step rnd*50\1, rnd*50\1, rnd*16/1
  end if  
next

at 0, ymax - 2*txth(&quot;Q&quot;) : print &quot; press any...&quot;
showpage
pause

xw = 350
yh = 250
bottle 0, 0, xw, yh
cls

for y = 0 to ymax step yh
  for x = 0 to xmax step xw
    pour x, y, xw, yh
    showpage
  next
next

at 0, ymax - 2*txth(&quot;Q&quot;): print &quot; press any...&quot;
showpage
pause
cls

for x = 0 to xmax step 25
  cls
  pour x, ymax\2 - yh\2, xw, yh
  showpage
  delay 10
next

at 0, ymax - 2*txth(&quot;Q&quot;) : print &quot;done, press any... &quot;
showpage
pause 

sub bottle(xleft, ytop, xwidth, yheight)
  local x, y
  dim screensection(xwidth, yheight)
  for y = ytop to (ytop + yheight - 1)
    for x = xleft to (xleft + xwidth - 1)
      screensection(x, y) = POINT(x, y)
    next
  next
end

sub pour(xoff, yoff, xwidth, yheight)
  local x, y
  for y = 0 to yheight
    for x = 0 to xwidth
      colr = screensection(x, y)
      pset x + xoff, y + yoff, colr
    next
  next
end </code></pre>
<h3 id="example-4-get-graphics-cursor-and-color">Example 4: Get graphics cursor and color</h3>
<pre><code>&#39; Note: POINT(x, y) returns the color of the pixel at x,y. But it&#39;s
&#39;       also possible to use POINT(0) and POINT(1) to return the current
&#39;       X,Y graphics position...:

&#39; Draw a little circle at the current graphics x,y position:
Sub show_point
  Local x = Point(0) &#39; This is where the last graphics command left us...
  Local y = Point(1)
  Local c
  c = Point(x, y)    &#39; Now we know also the color of that point...
  Circle x, y, 3 Color c FILLED &#39; (CIRCLE does not change current X,Y)
End Sub

&#39; Draw a box and show the corners with a circle:
Line 50, 50, 360, 50 Color 15:     show_point
Line STEP 0, +40 Color 14:         show_point
Line STEP -(360 - 50), 0 Color 15: show_point
Line STEP 0, -40 Color 14:         show_point
Showpage

&#39; Print text inside the box and change its colors:
x = Point(0) + 45
y = Point(1) + 8
text = &quot;That&#39;s the POINT !&quot;
x_dot = x + Txtw(text) - 6
y_dot = y + Txth(text) - 5
For b = 7 To 1 Step -1   &#39; b = Background color
  c = b Xor (16 - b)     &#39; c = Reversed Foreground color
  Color c, b: At x, y: Print &quot;That&#39;s the POINT !&quot;
  Circle x_dot, y_dot, 3 Color c FILLED &#39; Print a point under &#39;!&#39;
  Paint x - 2, y - 2, b  &#39; Paint the rest of the box
  Pause 1
  Showpage
Next
Pause</code></pre>
