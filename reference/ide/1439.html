<h1 id="chain">CHAIN</h1>
<blockquote>
<p>CHAIN source</p>
</blockquote>
<p>Compile and run the given source. <code>source</code> can be a file name, a line of code or an array of code. Use ENV to share variables with the parent process.</p>
<h3 id="example-1-using-constants">Example 1: Using constants</h3>
<p>Note: <code>\"</code> is used to create a quote inside the string</p>
<pre><code>Chain &quot;? \&quot;100 + 50 is: \&quot;; 100 + 50&quot; 

&#39; Output: 100 + 50 is: 150</code></pre>
<h3 id="example-2-using-variables">Example 2: Using variables</h3>
<pre><code>Env &quot;SB1=6&quot;
Env &quot;SB2=2&quot;
Chain &quot;? Env(\&quot;SB1\&quot;) ^ Env(\&quot;SB2\&quot;)&quot; 

&#39; Output: 36</code></pre>
<h3 id="example-3-using-an-array">Example 3: Using an array</h3>
<pre><code>Env &quot;SB1=3&quot;
  
Dim a()          
a &lt;&lt; &quot;x = Env(\&quot;SB1\&quot;)&quot;
a &lt;&lt; &quot;For i = 1 To 5&quot;
a &lt;&lt; &quot;? i * x; Spc(1);&quot;
a &lt;&lt; &quot;Next i&quot;

Chain a 

&#39; Output: 3 6 9 12 15</code></pre>
<h3 id="example-4-using-a-file-and-returning-a-value">Example 4: Using a file and returning a value</h3>
<pre><code>&#39; First we have to create a bas-file to show how chain works with files

&#39; Create an array
Env &quot;SB1=4&quot;  

Dim a()      
a &lt;&lt; &quot;x = Env(\&quot;SB1\&quot;)&quot;
a &lt;&lt; &quot;For i = 1 To 5&quot;
a &lt;&lt; &quot;? i * x; Spc(1);&quot;
a &lt;&lt; &quot;Next i&quot;
a &lt;&lt; &quot;Env \&quot;SB1=\&quot; + Str(i):&quot;    &#39; Return value using SB1

&#39; Write array to file
tsave(&quot;chaindemo.bas&quot;, a)

&#39; Peparations are done. Now a bas-file can be chained
Chain &quot;chaindemo.bas&quot; 

print
print &quot;Return value SB1 is: &quot;; Env(&quot;SB1&quot;); 

&#39; Output: 
&#39; 4 8 12 16 20
&#39; Return value SB1 is: 6</code></pre>
<h3 id="example-5">Example 5:</h3>
<pre><code>&#39; Create demo bas file (could be any SmallBASIC file):
Open &quot;chaindemo.bas&quot; For Output As #1
? #1, &quot;Sub count10(n)&quot;
? #1, &quot;  Local i&quot;
? #1, &quot;  Color 14&quot;
? #1, &quot;  For i = n To 10
? #1, &quot;    Print i; &quot; + Enclose(&quot;, &quot;) + &quot;;&quot; 
? #1, &quot;  Next i&quot;
? #1, &quot;  Env &quot; + Enclose(&quot;SB1=&quot;) + &quot; + Str(i) &#39; return value to parent
? #1, &quot;End Sub&quot;
? #1, 
? #1, &quot;Color 7: ? &quot; + Enclose(&quot;I&#39;m The Child Program...! I can count!&quot;) + &quot;: ?&quot;
? #1, 
? #1, &quot;n = Val(Env(&quot; + Enclose(&quot;SB1&quot;) + &quot;)) &#39; get value from parent&quot;
? #1, 
? #1, &quot;count10 n &#39; run sub-routine to count n to 10&quot;
? #1, 
? #1, &quot;?:?&quot;
Close #1

&#39; Load demo bas file into array:
Tload &quot;chaindemo.bas&quot;, lines
Env &quot;SB1=2&quot; &#39; Set value for child program (1..10)
&#39; Execute the demo bas file (the array):
Chain lines
&#39; now print the return value from child program:
Color 7:  ? &quot;I&#39;m The Parent Program...&quot;
?
Color 15: ? &quot;Child program returned value: &quot;; Env(&quot;SB1&quot;)</code></pre>
<h3 id="example-6-creating-an-eval-function">Example 6: Creating an eval function</h3>
<pre><code>&#39; Dedicated to MGA.
&#39; s is any legal SmallBASIC Math Expression as String, e.g. &quot;1 + 2 / 4&quot;
Func eval(s)
  Chain &quot;Env &quot; + Enclose(&quot;SBEVAL=&quot;) + &quot; + Str(&quot; + s + &quot;)&quot;
  eval = Val(Env(&quot;SBEVAL&quot;))
End Func

&#39; now run few demos:
? eval(&quot;1+2&quot;) 
? eval(&quot;Rad(45) * 2&quot;) 
? eval(&quot;PI / 2 + PI&quot;)
? eval(&quot;0b1111 * Pow(2, 4)&quot;)
? eval(&quot;Sin(2) * Tan(4) / Cos(6)&quot;)
? eval(&quot;1 + 2 / 4&quot;)
? eval(&quot;6 * (Pow(2, 4) * 8)&quot;)
? eval(&quot;Rad((45 * 3) - 20) * 2&quot;)</code></pre>
