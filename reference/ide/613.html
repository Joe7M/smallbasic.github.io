<h1 id="circle">CIRCLE</h1>
<blockquote>
<p>CIRCLE [STEP] x,y,r [,aspect [, color]] [COLOR color] [FILLED]</p>
</blockquote>
<p>Draws a circle (or an ellipse if the aspect is specified).</p>
<pre><code>
&#39; Aspect ratio is relation between width-height of a circle,
&#39; for example:
&#39; aspect = 1   = Perfect circle;
&#39; aspect = 1.5 = Stretch circle vertically (ellipse);
&#39; aspect = 0.5 = Stretch circle horizontally (ellipse);
&#39; let&#39;s draw perfect circles for the flower:
Const ASPECT_RATIO = 1
&#39; define parameters for main circle:
Const CENTER = [150, 150] &#39; [x, y] define the center point to draw a circle
Const RADIUS = 50         &#39; radius in pixels (pixel is a single point)
Const TEXT = &quot;Smile!&quot;     &#39; text to print in the middle of main circle
&#39; define parameters for leafs:
Const LEAF_RADIUS = 30    &#39; using smaller circles for leafs
&#39; center point for straight leafs (distance from main circle&#39;s center):
Const LEAF_CENTER_STRAIGHT   = RADIUS + LEAF_RADIUS
&#39; rotate center point for transverse leafs (distance as above):
Const LEAF_CENTER_TRANSVERSE = LEAF_CENTER_STRAIGHT * Sin(Rad(45))
&#39; Start the program:
Print &quot; Press any key to draw leaf by leaf...&quot;;
Pause
Cls
&#39; draw one invisible point to set the &#39;current position&#39;:
Pset CENTER Color 0
&#39; let&#39;s use shorter variable names (easier to read):
a = ASPECT_RATIO
r = LEAF_RADIUS
&#39; draw the straight leafs (relative to the &#39;current position&#39;):
c = LEAF_CENTER_STRAIGHT
Circle Step 0, -c, r, a Color 15 FILLED: Pause &#39; top
Circle Step 0, +c, r, a Color 14 FILLED: Pause &#39; down
Circle Step -c, 0, r, a Color 13 FILLED: Pause &#39; left
Circle Step +c, 0, r, a Color 12 FILLED: Pause &#39; right
&#39; draw the transverse leafs (relative to the &#39;current position&#39;):
c = LEAF_CENTER_TRANSVERSE
Circle Step -c, -c, r, a Color 11 FILLED: Pause &#39; top-left
Circle Step +c, +c, r, a Color  8 FILLED: Pause &#39; down-right
Circle Step +c, -c, r, a Color 10 FILLED: Pause &#39; top-right
Circle Step -c, +c, r, a Color  9 FILLED: Pause &#39; down-left
&#39; draw the main circle:
Circle CENTER, RADIUS, ASPECT_RATIO Color 14
&#39; print text in the middle of main circle:
At CENTER(0) - (Txtw(TEXT) \\ 2), CENTER(1) - (Txth(TEXT) \\ 2)
Color 15: Print TEXT;
Pause
</code></pre>
<pre><code>
&#39;Another aspect or circle.bas  SmallBASIC 0.12.2 [B+=MGA] 2016-03-16
&#39;with aspect it is not the same old circle
cx=xmax/2:cy=ymax/2:rface=250:reye=50:xle=cx-100:xre=cx+100:dir=1
while 1
  cls
  circle cx,cy,rface,1,14 filled
  circle xle,cy,reye,1-a,9 filled
  circle xre,cy,reye,1-a,9 filled
  circle cx,cy+130-a*50,100+a*50,.21+2*a,12 filled
  circle cx,cy+100-a*50,100+a*50,.21+a,14 filled
  at 0,0 :?a
  showpage
  delay 1
  a+=.002*dir
  if a=.26 then dir=dir*-1
  if a=-.1 then dir=dir*-1
wend
</code></pre>
<p>By default CIRCLE draws with anti-aliasing. For details, see: <a href="https://yellowsplash.wordpress.com/2009/10/23/fast-antialiased-circles-and-ellipses-from-xiaolin-wus-concepts/" target="_blank">Fast, Antialiased Circles and Ellipses</a></p>
<p>To turn off anti-aliasing, add this to the start of your program:</p>
<pre><code> option predef antialias off</code></pre>
