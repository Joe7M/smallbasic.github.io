<h1 id="sin">SIN</h1>
<blockquote>
<p>f = SIN (x)</p>
</blockquote>
<p>Calculate the Sine of <code>x</code>. <code>x</code> is in radians.</p>
<h3 id="example-1">Example 1</h3>
<pre><code>a = sin(0.5)
print a         &#39; Output: 0.4794255386042

b = asin(a)
print b         &#39; Output: 0.5</code></pre>
<h3 id="example-2-using-rad">Example 2: Using RAD</h3>
<p>The RAD function can be used to convert an angle in degrees to radians.</p>
<pre><code>alpha_in_degrees = 90
a = sin(rad(alpha_in_degrees))
print a                         &#39; Output: 1</code></pre>
<h3 id="example-3-drawing-a-spiral">Example 3: Drawing a spiral</h3>
<pre><code>&#39; Drawing spiral around point.bas SmallBASIC 0.12.2 [B+=MGA] 2016-03-13
&#39; using SIN and COS in a most common situation drawing at an angle from a point

cx = xmax\2 : cy = ymax\2

for degree = 0 to 360 step 10   &#39;zero is due East

  line cx, cy step 10 * (degree^.5) * cos(rad(degree)), 10 * (degree^.5) * sin(rad(degree)), rgb(128,128,degree/360 * 128 + 127)

  at 0,0: print spc(100)
  at 0,0
  
  if degree = 0 then
    print &quot; 0 degrees don&#39;t worry, you haven&#39;t seen nothing yet! press any key ...&quot;
  else
    print degree;&quot; degrees press any key ...&quot;
  end if
  
  pause
next

print &quot;Notice how the the drawing started due East for 0 degrees and&quot;
print &quot;as the angle increased the drawing went around clock-wise.&quot;

pause 

cls

&#39; now I am doing a LOGO like program and I want 0 degrees to be due North
&#39; due North is -90 degrees for clock-wise rotation so subtract 90 from degrees

for degree = 0 to 360 step 10
  
  line cx, cy step 10 * (degree^.5) * cos(rad(degree - 90)), 10 * (degree^.5) * sin(rad(degree - 90)), rgb(250,0,degree/360 * 128 + 127)

  at 0,0: print spc(100)
  at 0,0

  if degree = 0 then
    print &quot; 0 degrees don&#39;t worry, you haven&#39;t seen nothing yet! press any key ...&quot;
  else
    print degree;&quot; degrees press any key ...&quot;
  end if
  
  pause

next
print &quot;Notice how the drawing started due North because of the -90 adjustment made in SIN and COS calls&quot;</code></pre>
<h3 id="example-3-draw-a-polygon">Example 3: Draw a polygon</h3>
<pre><code>&#39; sin and cos use.bas  SmallBASIC 0.12.2 [B+=MGA] 2016-03-15
&#39;                    SIN and COS work as a team
&#39;
&#39; SIN and COS are used most for drawing points in circle around a central point.
&#39; Here we are going to divide a circle into n sections to draw an n sided polygon,
&#39; for instance, divide a circle into 3 equal parts and you get equilateral triangle (all sides =)
&#39; divide by 4 and get a square, by 5 a pentagon, 6 a hexagon.... all equal sides
&#39; I have to spend some time explaining why this works and a triangle diagram is really needed
&#39; but here goes, imagine a pie sitting on top of a given center point cx,cy
&#39; it is cut into n equal slices each piece has a central angle of 360/n or in radians 2*pi/n
&#39; a polygon just cuts the round parts off with straight lines to each end at outer edge.
&#39; remember COS is the leg adjacent to the angle in question divide by hypotenuse
&#39; or COS(inner angle) = adj/hyp
&#39; x is the adjacent side, the hypotenuse is the radius
&#39; so COS(inner angle) = x/radius
&#39; or radius * COS(angle) = x  multiple each side by radius
&#39; x = radius * COS(angle)
&#39; but remember this is distance relative to center point not a screen coordinate
&#39; to make absolute screen point it is: x = cx + radius*COS(angle)
&#39; SIN works out similar
&#39; SIN(inner angle) = opposite/hyp
&#39; and again y is opposite side (of inner angle) and the hypotenuse is the radius of circle
&#39; SIN(angle) = y/radius    multiply each side by radius
&#39; radius*SIN(angle) = y
&#39; y = radius * SIN(angle)
&#39; to make absolute screen point it is y = cy + radius*SIN(angle)
&#39; So going around a central point x=cx+radius*COS(angle),y=cy+radius*SIN(angle)
&#39; will locate points equal distance from center for equal sided polygon

cx = xmax / 2: cy = ymax / 2                &#39; here&#39;s our center move it anywhere on screen
&#39;cx=100 : cy = 500                          &#39; &lt;== try different center points
radius = 150

while 1
  cls
  circle cx, cy, 3, 1, 14                   &#39;this shows the central point in small yellow circle

  &#39; what polygon shall we draw?
  print &quot;A regular polygon is a many sided figure with equal sides.&quot;
  print &quot;A triangle is a 3 sided polygon, a square 4, a pentagon 5...&quot;
  input &quot;(&lt;=2 quits) Enter the number of sides for a polygon to draw &quot;; n
  
  if n &lt; 3 then end                         &#39; need at least 3 sides

  &#39; make the first point 0 degrees or directly East of center point (offset 0)
  &#39; you can off set the first point by some degree amount to spin the polygon by the offset
  
  offset = 0                                &#39; play with this to spin polygon
  &#39; offset = pi / 10 
  &#39; offset = pi / 18
  &#39; offset = pi / 36 
  
  for centralAngle = offset to 2*pi + offset step 2*pi / n
    &#39;2*pi is a circle (360 degrees) divide by n get inner angles of pie slices that&#39;s our step

    &#39;radius * cos(centralAngle) = our needed x coordinate to center point so add cx for absolute screen coordinate
    &#39;radius * sin(centralAngle) = our needed y coordinate to center point so add cy for absolute screen coordinate
    if centralAngle=0+offset then           &#39;first point location
      xstart = cx + radius * cos(centralAngle)
      ystart = cy + radius * sin(centralAngle) &#39;get the ball rolling by recording first point
    else
      line xstart, ystart, cx + radius * cos(centralAngle), cy + radius * sin(centralAngle)
      xstart = cx + radius * cos(centralAngle)
      ystart = cy + radius * sin(centralAngle)
    end if
    delay 300                               &#39;to show the progress of draw
  next
  s=&quot;press any key ...&quot;
  at cx - txtw(s) / 2, cy - txth(s) / 2:print s  &#39;this puts cue right where you will see it
  pause
wend
&#39;you are just a few simple steps away from spinning these polygons!</code></pre>
<h3 id="example-4-plot-sin-cos-and-tan">Example 4: Plot sin, cos and tan</h3>
<pre><code>&#39; sin-cos-tan.bas from FLTK samples
cls
at 0, ymax / 2 + txth(&quot;Q&quot;)

color 1:  print &quot;sin(x)&quot;
color 8:  print &quot;cos(x)&quot;
color 12: print &quot;tan(x)&quot;

line 0, ymax/2, xmax, ymax/2  &#39;central or x axis, ymax/4 is multiplier scaled to 1/4 of ymax
for i = 0 to xmax
   pset i, ymax/2 - sin(i * 2*pi / ymax) * ymax/4 color 1
   pset i, ymax/2 - cos(i * 2*pi / ymax) * ymax/4 color 8
   pset i, ymax/2 - tan(i * 2*pi / ymax) * ymax/4 color 12
next</code></pre>
